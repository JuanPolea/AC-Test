buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.0'
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.50'
        classpath 'org.jetbrains.kotlin:kotlin-serialization:1.9.21'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.20.0"
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.9'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
plugins {
    id 'com.android.application' version '8.1.4' apply false
    id 'com.android.library' version '8.1.4' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.21' apply false
}
subprojects { subProject ->
    subProject.apply plugin: 'kotlin-kapt'

    subProject.apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        debug = false
        toolVersion = "1.20.0"
        input = subProject.files("src")
        config = files("$rootDir/config/detekt/detekt.yml")

        reports {
            xml.enabled = false
            txt.enabled = false
            html.enabled = true
        }
    }

    configurations.all {
        resolutionStrategy.force 'org.objenesis:objenesis:2.6'
    }

    afterEvaluate {
        if (it.hasProperty('android')) {
            android {
                compileSdkVersion 34
                buildToolsVersion "33.0.1"

                defaultConfig {
                    minSdk 23
                    targetSdk 34
                    versionCode 1
                    versionName "0.0.1"
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }
                buildTypes {
                    release {
                        minifyEnabled false
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }

                testOptions {
                    unitTests.returnDefaultValues = true
                    unitTests.includeAndroidResources = true
                }
                sourceSets {
                    test {
                        resources {
                            srcDirs += ["${rootDir}/test/src/test/resources"]
                        }
                    }
                }
                packagingOptions {
                    exclude 'META-INF/*'
                }
            }
            dependencies {
                testImplementation libs.kotlin.coroutines
                testImplementation libs.test.kotlin.coroutines
                testImplementation libs.test.junit.ktx
                testImplementation libs.test.kotlin
                testImplementation libs.test.mockk
            }
        }
    }
}
